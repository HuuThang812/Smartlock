#include <WiFi.h>
#include <PubSubClient.h>

int in1 = 25;
int in2 = 26;
int enA = 27;

const char* ssid = "";
const char* password = "";
const char* mqtt_server = "";

WiFiClient espClient;
PubSubClient client(espClient);

int runTimeOpen = 4000;
int runTimeClose = 4000;

bool isRunning = false;
unsigned long startTime = 0;

enum DOOR_STATE { STOPPED,
                  OPENING,
                  CLOSING };
DOOR_STATE doorState = STOPPED;

void motorForward() {  // MỞ CỬA
  digitalWrite(in1, HIGH);
  digitalWrite(in2, LOW);
  ledcWrite(0, 200);
}

void motorBackward() {  // ĐÓNG CỬA
  digitalWrite(in1, LOW);
  digitalWrite(in2, HIGH);
  ledcWrite(0, 200);
}

void motorStop() {
  ledcWrite(0, 0);
  doorState = STOPPED;
  isRunning = false;
}

void callback(char* topic, byte* message, unsigned int length) {
  String msg = "";
  for (int i = 0; i < length; i++) msg += (char)message[i];

  if (msg == "OPEN") {
    motorForward();
    doorState = OPENING;
    isRunning = true;
    startTime = millis();
  }

  else if (msg == "CLOSE") {
    motorBackward();
    doorState = CLOSING;
    isRunning = true;
    startTime = millis();
  }

  else if (msg == "STOP") {
    motorStop();
  }
}

void setup() {
  Serial.begin(115200);

  pinMode(in1, OUTPUT);
  pinMode(in2, OUTPUT);

  ledcAttachPin(enA, 0);
  ledcSetup(0, 5000, 8);

  WiFi.begin(ssid, password);
  while (WiFi.status() != WL_CONNECTED) delay(500);

  client.setServer(mqtt_server, 1883);
  client.setCallback(callback);

  motorStop();
}
void reconnect() {
  while (!client.connected()) {
    if (client.connect("ESP32_Door")) {
      client.subscribe("door/control");
    } else delay(2000);
  }
}

void loop() {
  if (!client.connected()) reconnect();
  client.loop();

  if (isRunning) {
    unsigned long now = millis();

    if (doorState == OPENING && (now - startTime >= runTimeOpen)) {
      motorStop();
    }

    if (doorState == CLOSING && (now - startTime >= runTimeClose)) {
      motorStop();
    }
  }
}
